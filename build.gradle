buildscript {
    repositories {
        mavenCentral()
    }
    ext {
        springBootVersion = '1.2.7.RELEASE'
	springCoreVersion = '4.2.0.RELEASE'
	springSecurityVersion = '4.0.0.RELEASE'
        salixVersion = '1.0.0-M1'
    }
    dependencies {
        // spring boot
    	classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
	id "com.jfrog.bintray" version "1.5"
}

ext {
	environment = project.hasProperty('env') ? env : 'dev'
	salixVersion = '1.0.0-M1'
}

allprojects {
    repositories {
		mavenCentral()
		
		jcenter {
			url "http://jcenter.bintray.com/"
		}
    }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'spring-boot'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
	
	distTar.enabled = false
	distZip.enabled = false
	
	project.group = 'org.lukosan.salix'
	
	bootRepackage.enabled = false
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	applicationDefaultJvmArgs = ["-Dspring.profiles.active=" + environment ] // enable cmd line "env" -> spring profiles
	
	if(project.hasProperty('debug')) {
		applicationDefaultJvmArgs << "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=8000" // enable debugger (wait for connection on startup)
	} else {
		applicationDefaultJvmArgs << "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8000" // enable debugger (do not wait for connection)
	}
	
	configurations {
		all*.exclude group: 'org.slf4j', module: 'slf4j-simple' // get rid of slf4j
		all*.exclude group: 'ch.qos.logback', module: 'logback-classic' // get rid of logback-classic as it's greedy
		all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging' // exclude when using log4j
	}
	
	jar {
		baseName = project.name
		version =  salixVersion
	}
	
	springBoot {
		mainClass = 'org.lukosan.salix.Application'
	}

	task sourceJar(type: Jar) {
    		from sourceSets.main.allSource
    		classifier = 'sources'
	}
	javadoc {
		failOnError = false
	}
	task javadocJar(type: Jar, dependsOn: javadoc) {
    		classifier = 'javadoc'
    		from javadoc.destinationDir
	}
	artifacts {
		archives javadocJar
		archives sourceJar
	}

	publishing {
		publications {
			MyPublication(MavenPublication) {
				from components.java
				groupId 'org.lukosan.salix'
            			artifactId project.name
            			version salixVersion
				artifact sourceJar
            			artifact javadocJar
			}
		}
	}
	bintray {
		user = "$BINTRAY_USER"
		key = "$BINTRAY_API_KEY"
		publications = ['MyPublication']
//		configurations = ['archives']
		dryRun = false
		publish = true 
		pkg {
			repo = 'maven'
			name = project.name
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/lukosan/' + project.name + '.git'
			version {
				name = salixVersion
				released  = new Date()
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
